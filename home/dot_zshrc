# =============================================================================
# .zshrc - Zsh Configuration
# =============================================================================

# -----------------------------------------------------------------------------
# sheldon (plugin manager)
# -----------------------------------------------------------------------------
eval "$(sheldon source)"

# -----------------------------------------------------------------------------
# Pure prompt style
# -----------------------------------------------------------------------------
zstyle ':prompt:pure:path' color cyan

# -----------------------------------------------------------------------------
# Environment
# -----------------------------------------------------------------------------
export LC_CTYPE=UTF-8
export EDITOR=vim

# -----------------------------------------------------------------------------
# Completion
# -----------------------------------------------------------------------------
autoload -U compinit; compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'
zstyle ':completion:*' ignore-parents parent pwd ..
zstyle ':completion:*:sudo:*' command-path /usr/local/sbin /usr/local/bin /usr/sbin /usr/bin /sbin /bin /usr/X11R6/bin
zstyle ':completion:*:processes' command 'ps x -o pid,s,args'

# -----------------------------------------------------------------------------
# Options
# -----------------------------------------------------------------------------
setopt auto_cd
setopt correct
setopt nolistbeep
setopt auto_pushd
setopt hist_ignore_dups
setopt hist_ignore_all_dups
setopt append_history
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt extended_glob
setopt print_eight_bit

# -----------------------------------------------------------------------------
# History
# -----------------------------------------------------------------------------
HISTFILE=~/.zsh_history
HISTSIZE=100000
SAVEHIST=100000

# -----------------------------------------------------------------------------
# mise (runtime version manager)
# -----------------------------------------------------------------------------
eval "$(mise activate zsh)"

# -----------------------------------------------------------------------------
# direnv
# -----------------------------------------------------------------------------
eval "$(direnv hook zsh)"

# -----------------------------------------------------------------------------
# zoxide (smart cd)
# -----------------------------------------------------------------------------
eval "$(zoxide init zsh)"

# -----------------------------------------------------------------------------
# atuin (shell history)
# -----------------------------------------------------------------------------
. "$HOME/.atuin/bin/env"
eval "$(atuin init zsh)"

# -----------------------------------------------------------------------------
# fzf configuration
# -----------------------------------------------------------------------------
export FZF_DEFAULT_COMMAND='rg --files --hidden --glob "!.git"'
export FZF_DEFAULT_OPTS='
  --height 60%
  --border rounded
  --layout reverse
  --preview-window right:50%
  --bind ctrl-u:preview-half-page-up
  --bind ctrl-d:preview-half-page-down
  --color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8
  --color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc
  --color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8
'
export FZF_CTRL_T_OPTS="--preview 'bat --color=always --style=numbers --line-range :500 {}'"
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# -----------------------------------------------------------------------------
# bat configuration
# -----------------------------------------------------------------------------
export BAT_THEME="Catppuccin Mocha"

# -----------------------------------------------------------------------------
# Go
# -----------------------------------------------------------------------------
export GOPATH=$HOME/go
export PATH=$PATH:$GOROOT/bin:$GOPATH/bin

# -----------------------------------------------------------------------------
# PATH
# -----------------------------------------------------------------------------
export PATH="$HOME/.local/bin:$PATH"
export PATH="/opt/homebrew/opt/postgresql@15/bin:$PATH"

# =============================================================================
# Aliases
# =============================================================================

# -----------------------------------------------------------------------------
# Directory navigation
# -----------------------------------------------------------------------------
alias d='cd ~/Desktop'
alias dotfiles='cd ~/dotfiles'
alias W='cd ~/workspace'

# -----------------------------------------------------------------------------
# File operations
# -----------------------------------------------------------------------------
alias c='clear'
alias rm='rm -i'
alias mv='mv -i'
alias cp='cp -i'
alias ..='cd ..'
alias ls='ls -G'
alias la='ls -a'
alias ll='ls -l'
alias tree='tree -C'
alias mkdir='mkdir -p'
alias cat='bat'
export LSCOLORS=Cxfxcxdxbxegedabagacad

# -----------------------------------------------------------------------------
# Editor
# -----------------------------------------------------------------------------
alias vi='vim'
alias f='open .'
alias od='open ~/Desktop'

# -----------------------------------------------------------------------------
# Git
# -----------------------------------------------------------------------------
alias gs='git status'
alias gd='git diff'
alias ga='git add'
alias gc='gitmoji -c'

# -----------------------------------------------------------------------------
# Docker
# -----------------------------------------------------------------------------
alias dc='docker-compose'

# -----------------------------------------------------------------------------
# Tmux
# -----------------------------------------------------------------------------
alias t='tmux'

# -----------------------------------------------------------------------------
# Reload
# -----------------------------------------------------------------------------
alias zshrc='source ~/.zshrc'

# -----------------------------------------------------------------------------
# Global alias
# -----------------------------------------------------------------------------
alias -g G='| grep'

# =============================================================================
# Functions - ghq + fzf
# =============================================================================

function repo() {
  local dir
  dir=$(ghq list -p | fzf --preview 'bat --color=always --style=header,grid --line-range :80 {}/README.md 2>/dev/null || ls -la {}')
  if [[ -n "$dir" ]]; then
    cd "$dir"
  fi
}

function repo-browse() {
  local repo
  repo=$(ghq list | fzf --preview 'ghq list -p | grep {} | xargs ls -la')
  if [[ -n "$repo" ]]; then
    gh browse -R "$repo"
  fi
}

function get() {
  ghq get "$1" && repo
}

# =============================================================================
# Functions - fzf + ripgrep
# =============================================================================

function rgf() {
  local file line
  read -r file line <<<"$(rg --line-number --no-heading "$@" | \
    fzf --delimiter ':' \
        --preview 'bat --color=always --highlight-line {2} {1}' \
        --preview-window 'right:60%:+{2}-10' | \
    awk -F: '{print $1, $2}')"
  if [[ -n "$file" ]]; then
    vim "$file" +"$line"
  fi
}

function vf() {
  local file
  file=$(fzf --preview 'bat --color=always --style=numbers --line-range :500 {}')
  if [[ -n "$file" ]]; then
    vim "$file"
  fi
}

function cdf() {
  local dir
  dir=$(find . -type d 2>/dev/null | fzf --preview 'ls -la {}')
  if [[ -n "$dir" ]]; then
    cd "$dir"
  fi
}

# =============================================================================
# Functions - fzf + Git
# =============================================================================

function gb() {
  local branch
  branch=$(git branch -a --sort=-committerdate | \
    fzf --preview 'git log --oneline --graph --color=always {1} | head -50' | \
    sed 's/^[* ]*//' | sed 's/remotes\/origin\///')
  if [[ -n "$branch" ]]; then
    git checkout "$branch"
  fi
}

function gshow() {
  local commit
  commit=$(git log --oneline --graph --color=always | \
    fzf --ansi --preview 'echo {} | cut -d" " -f2 | xargs git show --color=always' | \
    awk '{print $2}')
  if [[ -n "$commit" ]]; then
    git show "$commit"
  fi
}

function gcp() {
  local commit
  commit=$(git log --oneline --all --graph --color=always | \
    fzf --ansi --preview 'echo {} | grep -o "[a-f0-9]\{7\}" | head -1 | xargs git show --color=always' | \
    grep -o "[a-f0-9]\{7\}" | head -1)
  if [[ -n "$commit" ]]; then
    git cherry-pick "$commit"
  fi
}

function gstash() {
  local stash
  stash=$(git stash list | \
    fzf --preview 'echo {} | cut -d: -f1 | xargs git stash show -p --color=always' | \
    cut -d: -f1)
  if [[ -n "$stash" ]]; then
    git stash apply "$stash"
  fi
}

function gadd() {
  local files
  files=$(git status -s | \
    fzf -m --preview 'echo {} | awk "{print \$2}" | xargs git diff --color=always' | \
    awk '{print $2}')
  if [[ -n "$files" ]]; then
    echo "$files" | xargs git add
    git status -s
  fi
}

# =============================================================================
# Functions - peco
# =============================================================================

function peco-kill() {
  local pid
  pid=$(ps aux | peco | awk '{print $2}')
  if [[ -n "$pid" ]]; then
    echo "Killing process $pid"
    kill "$pid"
  fi
}

function peco-ip() {
  ifconfig | grep 'inet ' | peco | awk '{print $2}'
}

# =============================================================================
# Functions - Docker
# =============================================================================

function dex() {
  local container
  container=$(docker ps --format "{{.Names}}" | peco)
  if [[ -n "$container" ]]; then
    docker exec -it "$container" "${1:-sh}"
  fi
}

function drmi() {
  local images
  images=$(docker images | tail -n +2 | peco | awk '{print $3}')
  if [[ -n "$images" ]]; then
    echo "$images" | xargs docker rmi
  fi
}

# =============================================================================
# Functions - Custom
# =============================================================================

function yo() {
  local model_flag=""
  if [ -n "$CLAUDE_CODE_MODEL" ]; then
    model_flag="--model $CLAUDE_CODE_MODEL"
  fi
  local cmd="claude --add-dir=$HOME/workspace/layerx/memo-tiger/obsidian $model_flag $@"
  echo "$cmd"
  eval "$cmd"
}

function oo() {
  cd "$HOME/workspace/layerx/memo-tiger/obsidian"
}

# =============================================================================
# Key bindings
# =============================================================================

# Ctrl+G: ghq + fzf でリポジトリ移動
bindkey -s '^g' 'repo\n'

# Ctrl+F: fzf でファイル検索
bindkey -s '^f' 'vf\n'

# Ctrl+B: fzf でブランチ切り替え
bindkey -s '^b' 'gb\n'
